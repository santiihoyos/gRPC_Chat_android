plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.santiihoyos.grpcchat"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'

        main {
            java.srcDirs 'src/main/kotlin'

            proto {
                srcDir '../protos'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.20.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.45.1'
        }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc { option 'lite' }
            }
            task.builtins {
                java { option 'lite' }
            }
        }
    }
}

dependencies {

    // APK dependencies here.
    implementation(libs.kotlin)
    implementation(libs.androidx.core)
    implementation(libs.androidx.appCompat)
    implementation(libs.androidx.constraintLayout)
    implementation(libs.google.materialComponents)
    implementation(libs.androidx.navigation.ui)
    implementation(libs.androidx.navigation.fragment)

    implementation 'io.grpc:grpc-android:1.45.1'
    implementation 'io.grpc:grpc-okhttp:1.45.1'
    implementation("io.grpc:grpc-stub:1.45.1")
    implementation("io.grpc:grpc-protobuf:1.36.0")
    implementation("com.google.protobuf:protobuf-java:3.20.0")
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
